#!/usr/bin/env bds

# 
# Get AWS S3 parameters from $HOME/.bds/aws_test_bucket.config
# 

# Load an AWS config 'name' file
string{} getAwsInfo() {
	awsBucketFile := 'aws_test.config'
	bdsHomeDir := getVar('BDS_HOME', "$HOME/.bds")
	file := "$bdsHomeDir/$awsBucketFile"
	if( ! file.exists() ) error "Cannot file AWS bucket test information file '$file'"
	return file.config()
}

string getAwsBucketName() return getAwsInfo(){'bucket'}.trim()

string getAwsRegion() return getAwsInfo(){'region'}.trim()

taskResources := getAwsInfo()
awsRegion := taskResources{'region'}
bucket := taskResources{'bucket'}
cloudQueueNamePrefix := taskResources{'cloudQueueNamePrefix'}

test := programName.removeExt()
s3base := "s3://$bucket/tmp/bds/$test"
tmpDir := getVar('TMPDIR', '/tmp')

# Create a file in S3 if it doesn't exist
void createS3(string s3file) {
	createS3(s3file, 'OK')
}

void createS3(string s3file, string text) {
	if( s3file.exists() ) {
		println "CreateS3: OK, file '$s3file' exists, nothing done"
	} else {
		println "CreateS3: Uploading file '$s3file'"
		s3file.write(text)
	}
}
